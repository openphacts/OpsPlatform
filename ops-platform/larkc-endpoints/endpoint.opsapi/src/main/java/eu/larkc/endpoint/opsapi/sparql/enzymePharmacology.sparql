PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX chembl: <http://rdf.farmbio.uu.se/chembl/onto/#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX chemspider: <http://rdf.chemspider.com/#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
SELECT ?target_name ?target_organism ?compound_name ?csid_uri ?smiles ?inchi ?inchi_key ?molweight ?num_ro5_violations ?assay_organism ?std_type ?relation ?std_value ?std_unit WHERE { 
	GRAPH <http://purl.uniprot.org/enzyme/inference> {
		?subclass rdfs:subClassOf <INPUT_URI> 
	}
	GRAPH <http://data.kasabi.com/dataset/chembl-rdf> {
		?chembl_uri skos:exactMatch ?subclass;
			dc:title ?target_name;
			chembl:organism ?target_organism . 
		?assay_uri chembl:hasTarget ?chembl_uri ;
			chembl:organism ?assay_organism .
		?activity_uri chembl:onAssay ?assay_uri ;
			chembl:type ?std_type ;
			chembl:relation ?relation ;
			chembl:standardValue ?std_value ;
			chembl:standardUnits ?std_unit ;
			chembl:forMolecule ?compound_uri .
		?compound_uri owl:equivalentClass ?equiv_uri . 
		?equiv_uri skos:exactMatch ?csid_uri .
		OPTIONAL { ?compound_uri sio:CHEMINF_000200 _:node1 . 
			_:node1 a sio:CHEMINF_000314 ;
				sio:SIO_000300 ?num_ro5_violations }
		OPTIONAL { ?compound_uri sio:CHEMINF_000200 _:node2 . 
			_:node2 a sio:CHEMINF_000198 ;
				sio:SIO_000300 ?molweight }
	}
	GRAPH <http://www.chemspider.com> {
		?csid_uri chemspider:inchi ?inchi;
			chemspider:inchikey ?inchi_key;
			chemspider:smiles ?smiles . 
	}
	GRAPH <http://www.conceptwiki.org> {
		?compound_cw skos:exactMatch ?csid_uri ; 
			skos:prefLabel ?compound_name .
	}
}